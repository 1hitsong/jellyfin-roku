import "pkg:/source/enums/AnimationControl.bs"
import "pkg:/source/enums/AnimationState.bs"
import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/MenuType.bs"
import "pkg:/source/enums/TaskControl.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    overlay = m.top.findNode("overlay")
    overlay.color = ColorPalette.DARKGREY

    background = m.top.findNode("background")
    background.blendColor = ColorPalette.LIGHTGREY

    ' group = m.global.sceneManager.callFunc("getActiveScene")
    ' group.lastFocus = m.buttons

    m.selectedFavoriteItem = m.top.findNode("selectedFavoriteItem")

    m.selectedItem = 0

    m.filterMenu = m.top.findNode("filterMenu")
    m.filterMenu.focusBitmapBlendColor = ColorPalette.HIGHLIGHT
    m.filterMenu.focusFootprintBlendColor = ColorPalette.LIGHTHIGHLIGHT
    m.filterMenu.focusedColor = ColorPalette.WHITE

    filterOptionsBackdrop = m.top.findNode("filterOptionsBackdrop")
    filterOptionsBackdrop.color = ColorPalette.LIGHTGREY

    m.menus = []
    m.menus.push(m.filterMenu)

    m.filterOptions = m.top.findNode("filterOptions")
    m.filterOptions.focusBitmapBlendColor = ColorPalette.HIGHLIGHT
    m.filterOptions.focusFootprintBlendColor = ColorPalette.LIGHTHIGHLIGHT
    m.filterOptions.focusedColor = ColorPalette.WHITE

    m.filterMenu.observeField("itemFocused", "onFilterFocusChange")

    m.filterNames = []

    ' Animation
    m.fadeAnim = m.top.findNode("fadeAnim")
    m.fadeOutAnimOpacity = m.top.findNode("outOpacity")
    m.fadeInAnimOpacity = m.top.findNode("inOpacity")
    m.showChecklistAnimation = m.top.findNode("showChecklistAnimation")
    m.hideChecklistAnimation = m.top.findNode("hideChecklistAnimation")
end sub

sub showChecklist()
    if m.filterOptions.opacity = 0
        if m.showChecklistAnimation.state = AnimationState.STOPPED
            m.showChecklistAnimation.control = AnimationControl.START
        end if
    end if
end sub

sub hideChecklist()
    if m.filterOptions.opacity = 1
        if m.hideChecklistAnimation.state = AnimationState.STOPPED
            m.hideChecklistAnimation.control = AnimationControl.START
        end if
    end if
end sub

sub onFilterFocusChange()
    if not isFilterMenuDataValid()
        hideChecklist()
        return
    end if

    if m.filterMenu.content.getChild(m.filterMenu.itemFocused).getChildCount() > 0
        showChecklist()
    else
        hideChecklist()
    end if

    m.filterOptions.content = m.filterMenu.content.getChild(m.filterMenu.itemFocused)
    if isValid(m.filterMenu.content.getChild(m.filterMenu.itemFocused).checkedState)
        m.filterOptions.checkedState = m.filterMenu.content.getChild(m.filterMenu.itemFocused).checkedState
    else
        m.filterOptions.checkedState = []
    end if
end sub

' Check if data for Filter Menu is valid
function isFilterMenuDataValid() as boolean
    if not isValid(m.filterMenu) or not isValid(m.filterMenu.content) or not isValid(m.filterMenu.itemFocused)
        return false
    end if

    if not isValid(m.filterMenu.content.getChild(m.filterMenu.itemFocused))
        return false
    end if

    return true
end function

sub optionsSet()
    ' Filter Tab
    if isValid(m.top.options.filter)
        filterContent = CreateObject("roSGNode", "ContentNode")
        index = 0
        m.selectedFilterIndex = 0

        for each filterItem in m.top.options.filter
            entry = filterContent.CreateChild("OptionNode")
            entry.title = filterItem.Title
            entry.name = filterItem.Name
            entry.delimiter = filterItem.Delimiter

            if isValid(filterItem.options)
                for each filterItemOption in filterItem.options
                    entryOption = entry.CreateChild("ContentNode")
                    entryOption.title = toString(filterItemOption)
                end for
                entry.checkedState = filterItem.checkedState
            end if

            m.filterNames.push(filterItem.Name)
            if isValid(filterItem.selected) and filterItem.selected
                m.selectedFilterIndex = index
            end if
            index = index + 1
        end for
        m.menus[0].content = filterContent
        m.menus[0].checkedItem = m.selectedFilterIndex
    else
        filterContent = CreateObject("roSGNode", "ContentNode")
        entry = filterContent.CreateChild("ContentNode")
        entry.title = "All"
        m.filterNames.push("All")
        m.menus[0].content = filterContent
        m.menus[0].checkedItem = 0
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if key = KeyCode.DOWN
        m.menus[m.selectedItem].setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.menus[m.selectedItem]
        m.menus[m.selectedItem].drawFocusFeedback = true

        'If user presses down from button menu, focus first item.  If OK, focus checked item
        if key = KeyCode.DOWN
            m.menus[m.selectedItem].jumpToItem = 0
        else
            m.menus[m.selectedItem].jumpToItem = m.menus[m.selectedItem].itemSelected
        end if

        return true
    end if

    if key = KeyCode.RIGHT
        if not m.menus[m.selectedItem].isInFocusChain() then return false

        ' Handle Filter screen
        if m.selectedItem = 0
            if not isFilterMenuDataValid() then return false
            ' If filter has no options, select it
            if m.filterMenu.content.getChild(m.filterMenu.itemFocused).getChildCount() = 0
                return true
            end if

            ' Selected filter has options, move cursor to it
            m.filterOptions.setFocus(true)
            m.menus[m.selectedItem].setFocus(false)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.filterOptions
            return true
        end if
    end if

    if key = KeyCode.DOWN
        if not isFilterMenuDataValid() then return false

        if m.menus[m.selectedItem].isInFocusChain()
            ' Handle Filter screen
            if m.selectedItem = 0
                ' Selected filter has options, move cursor to it
                if m.filterMenu.content.getChild(m.filterMenu.itemFocused).getChildCount() > 0
                    m.menus[m.selectedItem].setFocus(false)
                    m.filterOptions.setFocus(true)
                    group = m.global.sceneManager.callFunc("getActiveScene")
                    group.lastFocus = m.filterOptions
                    return true
                end if
            end if
        end if
    end if

    if key = KeyCode.LEFT
        ' User wants to escape filter options
        if m.filterOptions.isInFocusChain()
            m.filterOptions.setFocus(false)
            m.menus[m.selectedItem].setFocus(true)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.menus[m.selectedItem]
            return true
        end if
    end if

    if key = KeyCode.OK
        if m.menus[m.selectedItem].isInFocusChain()
            ' Handle Filter screen
            if m.selectedItem = MenuType.FILTER
                if not isFilterMenuDataValid() then return false
                ' If filter has no options, select it
                if m.filterMenu.content.getChild(m.filterMenu.itemFocused).getChildCount() = 0
                    m.menus[0].checkedItem = m.menus[0].itemSelected
                    m.selectedFilterIndex = m.menus[0].itemSelected
                    m.top.filter = m.filterNames[m.selectedFilterIndex]
                    m.top.filterOptions = {}
                    return true
                end if

                ' Selected filter has options, move cursor to it
                m.filterOptions.setFocus(true)
                m.menus[m.selectedItem].setFocus(false)
                group = m.global.sceneManager.callFunc("getActiveScene")
                group.lastFocus = m.filterOptions
                return true
            end if
        end if

        ' User pressed OK from inside the filter's options
        if m.filterOptions.isInFocusChain()
            selectedOptions = []
            for i = 0 to m.filterOptions.checkedState.count() - 1
                if m.filterOptions.checkedState[i]
                    selectedValue = toString(m.filterOptions.content.getChild(i).title)
                    selectedOptions.push(selectedValue)
                end if
            end for

            if selectedOptions.Count() > 0
                m.menus[0].checkedItem = m.menus[0].itemFocused
                m.selectedFilterIndex = m.menus[0].itemFocused
                m.top.filter = m.filterMenu.content.getChild(m.filterMenu.itemFocused).Name

                newFilter = {}
                newFilter[m.top.filter] = selectedOptions.join(m.filterMenu.content.getChild(m.filterMenu.itemFocused).delimiter)
                m.top.filterOptions = newFilter
            else
                m.menus[0].checkedItem = 0
                m.selectedFilterIndex = 0
                m.top.filter = m.filterNames[0]
                m.top.filterOptions = {}
            end if

            m.filterMenu.content.getChild(m.filterMenu.itemFocused).checkedState = m.filterOptions.checkedState

            return true
        end if
        return true
    end if

    if key = KeyCode.OPTIONS
        hideChecklist()
        m.menus[m.selectedItem].visible = true ' Show Filter contents in case hidden by favorite button
        m.menus[m.selectedItem].drawFocusFeedback = false
        return false
    end if

    return false
end function
