import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.top.setFocus(false)

    m.alphaText = m.top.findNode("alphaText")
    m.alphaMenu = m.top.findNode("alphaMenu")

    m.alphaMenu.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.alphaMenu.focusFootprintBlendColor = ColorPalette.TRANSPARENT

    m.alphaMenu.setFocus(false)

    ' show mic icon above alpha menu if remote supports voice commands
    if m.global.device.hasVoiceRemote
        alphaMic = m.top.findNode("alphaMic")
        alphaMic.visible = true

        micText = m.top.findNode("alphaMicText")
        micText.font.size = 22
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false
    if not m.alphaMenu.isInFocusChain() then return false

    if key = KeyCode.OK
        child = m.alphaText.getChild(m.alphaMenu.itemFocused)
        m.top.letterSelected = child.title
        return true
    end if

    if key = KeyCode.UP
        if m.alphaMenu.itemFocused = 0
            m.alphaMenu.jumpToItem = m.alphaMenu.numRows - 1
            return true
        end if
    end if

    if key = KeyCode.DOWN
        if m.alphaMenu.itemFocused = m.alphaMenu.numRows - 1
            m.alphaMenu.jumpToItem = 0
            return true
        end if
    end if

    return false
end function
