import "pkg:/source/utils/misc.bs"
import "pkg:/source/constants/HomeRowItemSizes.bs"

sub init()
    m.top.itemComponentName = "HomeItem"
    ' how many rows are visible on the screen
    m.top.numRows = 2

    m.top.rowFocusAnimationStyle = "fixedFocusWrap"
    m.top.vertFocusAnimationStyle = "fixedFocus"

    m.top.showRowLabel = [true]
    m.top.rowLabelOffset = [0, 20]
    m.top.showRowCounter = [true]

    m.homeSections = {}

    updateSize()

    m.top.setfocus(true)

    m.top.observeField("rowItemSelected", "itemSelected")

    ' Load the Libraries from API via task
    m.LoadLibrariesTask = createObject("roSGNode", "LoadItemsTask")
    m.LoadLibrariesTask.observeField("content", "onLibrariesLoaded")

    ' set up tesk nodes for other rows
    m.LoadContinueWatchingTask = createObject("roSGNode", "LoadItemsTask")
    m.LoadContinueWatchingTask.itemsToLoad = "continue"

    m.LoadNextUpTask = createObject("roSGNode", "LoadItemsTask")
    m.LoadNextUpTask.itemsToLoad = "nextUp"

    m.LoadOnNowTask = createObject("roSGNode", "LoadItemsTask")
    m.LoadOnNowTask.itemsToLoad = "onNow"

    m.LoadFavoritesTask = createObject("roSGNode", "LoadItemsTask")
    m.LoadFavoritesTask.itemsToLoad = "favorites"
end sub

sub loadLibraries()
    m.LoadLibrariesTask.control = "RUN"
end sub

sub updateSize()
    m.top.translation = [111, 180]
    itemHeight = 330

    'Set width of Rows to cut off at edge of Safe Zone
    m.top.itemSize = [1703, itemHeight]

    ' spacing between rows
    m.top.itemSpacing = [0, 105]

    ' spacing between items in a row
    m.top.rowItemSpacing = [20, 0]

    m.top.visible = true
end sub

' processUserSections: Loop through user's chosen home section settings and generate the content for each row
'
' @return {dynamic} content node filled with child nodes for each row
function processUserSections() as dynamic
    loadedSections = 0
    content = CreateObject("roSGNode", "ContentNode")

    ' Add sections in order based on user settings
    for i = 0 to 6
        sectionName = LCase(m.global.session.user.settings["homesection" + i.toStr()])
        sectionLoaded = addHomeSection(content, sectionName)

        ' Count how many sections with data are loaded
        if sectionLoaded then loadedSections++

        ' If 2 sections with data are loaded or we're at the end of the web client section data, consider the home view loaded
        if not m.global.app_loaded
            if loadedSections = 2 or i = 6
                m.top.signalBeacon("AppLaunchComplete") ' Roku Performance monitoring
                m.global.app_loaded = true
            end if
        end if
    end for

    ' Favorites isn't an option on Web settings, so we must manually add it for now
    addHomeSection(content, "favorites")

    return content
end function


' onLibrariesLoaded: Handler when LoadLibrariesTask returns data
'
sub onLibrariesLoaded()
    ' save data for other functions
    m.libraryData = m.LoadLibrariesTask.content
    m.LoadLibrariesTask.unobserveField("content")
    m.LoadLibrariesTask.content = []

    content = processUserSections()

    setRowItemSizes()

    m.top.content = content
end sub

' Removes a home section from the home rows
sub removeHomeSection(sectionType as string)
    sectionName = LCase(sectionType)

    removedSection = m.homeSections[sectionName]

    if not isValid(removedSection) then return
    if not isValid(removedSection.index) then return

    m.homeSections.Delete(sectionName)

    for each section in m.homeSections
        if m.homeSections[section].index > removedSection.index
            m.homeSections[section].index--
        end if
    end for

    m.top.content.removeChildIndex(removedSection.index)

    setRowItemSizes()
end sub

' setRowItemSizes: Loops through all home sections and sets the correct item sizes per row
'
sub setRowItemSizes()
    newSizeArray = CreateObject("roArray", m.homeSections.count(), false)

    for each section in m.homeSections
        newSizeArray[m.homeSections[section].index] = m.homeSections[section].imagesize
    end for

    m.top.rowItemSize = newSizeArray
end sub

' Adds a new home section to the home rows.
' Returns a boolean indicating whether the section was handled.
function addHomeSection(content as dynamic, sectionName as string) as boolean
    ' Poster size library items
    if sectionName = "livetv"
        createLiveTVRow(content)
        return true
    end if

    ' Poster size library items
    if sectionName = "smalllibrarytiles"
        createLibraryRow(content)
        return true
    end if

    ' Continue Watching items
    if sectionName = "resume"
        createContinueWatchingRow(content)
        return true
    end if

    ' Next Up items
    if sectionName = "nextup"
        createNextUpRow(content)
        return true
    end if

    ' Latest items in each library
    if sectionName = "latestmedia"
        createLatestInRows(content)
        return true
    end if

    ' Favorite Items
    if sectionName = "favorites"
        createFavoritesRow(content)
        return true
    end if

    return false
end function

' Create a row displaying the user's libraries
sub createLibraryRow(content as dynamic)
    ' Ensure we have data
    if not isValidAndNotEmpty(m.libraryData) then return

    mediaRow = content.CreateChild("HomeRow")
    mediaRow.title = tr("My Media")

    sectionIndex = m.homeSections.count()

    isUpdate = false

    if m.homeSections.doesExist("library")
        sectionIndex = m.homeSections.library.index
        isUpdate = true
    end if

    m.homeSections.AddReplace("library", {
        imageSize: homeRowItemSizes.WIDE_POSTER,
        index: sectionIndex
    })

    filteredMedia = filterNodeArray(m.libraryData, "id", m.global.session.user.configuration.MyMediaExcludes)
    for each item in filteredMedia
        mediaRow.appendChild(item)
    end for

    if isUpdate
        m.top.content.replaceChild(mediaRow, m.homeSections.library.index)
        return
    end if

    content.appendChild(mediaRow)
end sub

' Create a row displaying latest items in each of the user's libraries
sub createLatestInRows(content as dynamic)
    ' Ensure we have data
    if not isValidAndNotEmpty(m.libraryData) then return

    ' create a "Latest In" row for each library
    filteredLatest = filterNodeArray(m.libraryData, "id", m.global.session.user.configuration.LatestItemsExcludes)
    for each lib in filteredLatest
        if lib.collectionType <> "boxsets" and lib.collectionType <> "livetv" and lib.json.CollectionType <> "Program"

            sectionName = "latestin" + LCase(lib.name).Replace(" ", "")

            if not m.homeSections.doesExist(sectionName)
                latestInRow = content.CreateChild("HomeRow")
                latestInRow.title = tr("Latest in") + " " + lib.name + " >"
                sectionIndex = m.homeSections.count()
            else
                sectionIndex = m.homeSections[sectionName].index
            end if

            imagesize = homeRowItemSizes.WIDE_POSTER

            if LCase(lib.collectionType) = "movies"
                imagesize = homeRowItemSizes.MOVIE_POSTER
            else if LCase(lib.collectionType) = "music"
                imagesize = homeRowItemSizes.MUSIC_ALBUM
            end if

            m.homeSections.AddReplace(sectionName, {
                imageSize: imagesize,
                index: sectionIndex
            })

            loadLatest = createObject("roSGNode", "LoadItemsTask")
            loadLatest.itemsToLoad = "latest"
            loadLatest.itemId = lib.id

            metadata = { "title": lib.name }
            metadata.Append({ "contentType": lib.json.CollectionType })
            loadLatest.metadata = metadata

            loadLatest.observeField("content", "updateLatestItems")
            loadLatest.control = "RUN"
        end if
    end for
end sub

' Create a row displaying the live tv now on section
sub createLiveTVRow(content as dynamic)
    if not m.homeSections.doesExist("livetv")
        contentRow = content.CreateChild("HomeRow")
        contentRow.title = tr("On Now")
    end if

    sectionIndex = m.homeSections.count()

    if m.homeSections.doesExist("livetv")
        sectionIndex = m.homeSections.livetv.index
    end if

    m.homeSections.AddReplace("livetv", {
        imageSize: homeRowItemSizes.WIDE_POSTER,
        index: sectionIndex
    })

    m.LoadOnNowTask.observeField("content", "updateOnNowItems")
    m.LoadOnNowTask.control = "RUN"
end sub

' Create a row displaying items the user can continue watching
sub createContinueWatchingRow(content as dynamic)
    if not m.homeSections.doesExist("resume")
        continueWatchingRow = content.CreateChild("HomeRow")
        continueWatchingRow.title = tr("Continue Watching")
    end if
    sectionIndex = m.homeSections.count()

    if m.homeSections.doesExist("resume")
        sectionIndex = m.homeSections.resume.index
    end if

    m.homeSections.AddReplace("resume", {
        imageSize: homeRowItemSizes.WIDE_POSTER,
        index: sectionIndex
    })

    ' Load the Continue Watching Data
    m.LoadContinueWatchingTask.observeField("content", "updateContinueWatchingItems")
    m.LoadContinueWatchingTask.control = "RUN"
end sub

' Create a row displaying next episodes up to watch
sub createNextUpRow(content as dynamic)
    if not m.homeSections.doesExist("nextup")
        nextUpRow = content.CreateChild("HomeRow")
        nextUpRow.title = tr("Next Up >")
    end if
    sectionIndex = m.homeSections.count()

    if m.homeSections.doesExist("nextup")
        sectionIndex = m.homeSections.nextup.index
    end if

    m.homeSections.AddReplace("nextup", {
        imageSize: homeRowItemSizes.WIDE_POSTER,
        index: sectionIndex
    })

    ' Load the Next Up Data
    m.LoadNextUpTask.observeField("content", "updateNextUpItems")
    m.LoadNextUpTask.control = "RUN"
end sub

' Create a row displaying items from the user's favorites list
sub createFavoritesRow(content as dynamic)
    if not m.homeSections.doesExist("favorites")
        favoritesRow = content.CreateChild("HomeRow")
        favoritesRow.title = tr("Favorites")
    end if

    sectionIndex = m.homeSections.count()

    if m.homeSections.doesExist("favorites")
        sectionIndex = m.homeSections.favorites.index
    end if

    m.homeSections.AddReplace("favorites", {
        imageSize: homeRowItemSizes.WIDE_POSTER,
        index: sectionIndex
    })

    ' Load the Favorites Data
    m.LoadFavoritesTask.observeField("content", "updateFavoritesItems")
    m.LoadFavoritesTask.control = "RUN"
end sub

' Update home row data
sub updateHomeRows()
    processUserSections()
    setRowItemSizes()
end sub

sub updateFavoritesItems()
    itemData = m.LoadFavoritesTask.content
    m.LoadFavoritesTask.unobserveField("content")
    m.LoadFavoritesTask.content = []

    if itemData = invalid then return

    rowIndex = m.homeSections.favorites.index

    if itemData.count() < 1
        removeHomeSection("favorites")
        return
    else
        ' remake row using the new data
        row = CreateObject("roSGNode", "HomeRow")
        row.title = tr("Favorites")

        for each item in itemData
            usePoster = true

            if lcase(item.type) = "episode" or lcase(item.type) = "audio" or lcase(item.type) = "musicartist"
                usePoster = false
            end if

            item.usePoster = usePoster
            item.imageWidth = row.imageWidth
            row.appendChild(item)
        end for

        if isValid(m.top.content.getChild(rowIndex))
            m.top.content.replaceChild(row, rowIndex)
        else

            for each section in m.homeSections
                if m.homeSections[section].index >= m.top.content.getChildCount()
                    m.homeSections[section].index++
                end if
            end for

            m.homeSections.favorites.index = m.top.content.getChildCount()
            m.top.content.insertChild(row, m.top.content.getChildCount())
        end if
    end if
end sub

sub updateContinueWatchingItems()
    itemData = m.LoadContinueWatchingTask.content
    m.LoadContinueWatchingTask.unobserveField("content")
    m.LoadContinueWatchingTask.content = []

    if itemData = invalid then return

    if itemData.count() < 1
        removeHomeSection("resume")
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = tr("Continue Watching")

    for each item in itemData
        if isValid(item.json) and isValid(item.json.UserData) and isValid(item.json.UserData.PlayedPercentage)
            item.PlayedPercentage = item.json.UserData.PlayedPercentage
        end if

        item.usePoster = row.usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    ' replace the old row
    m.top.content.replaceChild(row, m.homeSections.resume.index)
end sub

sub updateNextUpItems()
    itemData = m.LoadNextUpTask.content
    m.LoadNextUpTask.unobserveField("content")
    m.LoadNextUpTask.content = []
    m.LoadNextUpTask.control = "STOP"

    if itemData = invalid then return

    if itemData.count() < 1
        removeHomeSection("nextup")
        return
    else
        ' remake row using the new data
        row = CreateObject("roSGNode", "HomeRow")
        row.title = tr("Next Up") + " >"
        for each item in itemData
            item.usePoster = row.usePoster
            item.imageWidth = row.imageWidth
            row.appendChild(item)
        end for

        ' replace the old row
        m.top.content.replaceChild(row, m.homeSections.nextup.index)
    end if
end sub

sub updateLatestItems(msg)
    itemData = msg.GetData()

    node = msg.getRoSGNode()
    node.unobserveField("content")
    node.content = []

    if itemData = invalid then return

    sectionName = "latestin" + LCase(node.metadata.title).Replace(" ", "")

    if itemData.count() < 1
        removeHomeSection(sectionName)
        return
    else
        ' remake row using new data
        row = CreateObject("roSGNode", "HomeRow")
        row.title = tr("Latest in") + " " + node.metadata.title + " >"
        row.usePoster = true

        for each item in itemData
            item.usePoster = row.usePoster
            item.imageWidth = m.homeSections[sectionName].imageSize[0]
            row.appendChild(item)
        end for

        rowIndex = m.homeSections[sectionName].index

        if isValid(rowIndex) and isValid(m.top.content.getChild(rowIndex))
            ' Replace the old row
            m.top.content.replaceChild(row, rowIndex)
        else
            firstLatestHomeSectionIndex = m.homeSections.count() - 1

            for each section in m.homeSections
                if LCase(Left(section, 6)) = "latest"
                    if m.homeSections[section].index < firstLatestHomeSectionIndex
                        firstLatestHomeSectionIndex = m.homeSections[section].index
                    end if
                end if
            end for

            for each section in m.homeSections
                if m.homeSections[section].index >= firstLatestHomeSectionIndex
                    m.homeSections[section].index++
                end if
            end for

            m.homeSections[sectionName].index = firstLatestHomeSectionIndex
            m.top.content.insertChild(row, firstLatestHomeSectionIndex)
        end if

        setRowItemSizes()


    end if
end sub

sub updateOnNowItems()
    itemData = m.LoadOnNowTask.content
    m.LoadOnNowTask.unobserveField("content")
    m.LoadOnNowTask.content = []

    if itemData = invalid then return

    if itemData.count() < 1
        removeHomeSection("livetv")
        return
    else
        ' remake row using the new data
        row = CreateObject("roSGNode", "HomeRow")
        row.title = tr("On Now")
        row.imageWidth = homeRowItemSizes.WIDE_POSTER[0]
        for each item in itemData
            row.usePoster = false

            if (not isValid(item.thumbnailURL) or item.thumbnailURL = "") and isValid(item.json) and isValid(item.json.imageURL)
                item.thumbnailURL = item.json.imageURL
                row.usePoster = true
                row.imageWidth = homeRowItemSizes.MOVIE_POSTER[0]

                m.homeSections.livetv.imageSize = homeRowItemSizes.MOVIE_POSTER
            end if

            item.usePoster = row.usePoster
            item.imageWidth = row.imageWidth
            row.appendChild(item)
        end for

        ' replace the old row
        m.top.content.replaceChild(row, m.homeSections.livetv.index)

        ' We may now have different poster sizes. Reset the row item sizes
        setRowItemSizes()

    end if
end sub

sub itemSelected()
    m.selectedRowItem = m.top.rowItemSelected

    m.top.selectedItem = m.top.content.getChild(m.top.rowItemSelected[0]).getChild(m.top.rowItemSelected[1])

    'Prevent the selected item event from double firing
    m.top.selectedItem = invalid
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if press
        if key = "play"
            print "play was pressed from homerow"
            itemToPlay = m.top.content.getChild(m.top.rowItemFocused[0]).getChild(m.top.rowItemFocused[1])
            if isValid(itemToPlay)
                m.top.quickPlayNode = itemToPlay
            end if
            return true
        else if key = "replay"
            m.top.jumpToRowItem = [m.top.rowItemFocused[0], 0]
            return true
        end if
    end if
    return false
end function

function filterNodeArray(nodeArray as object, nodeKey as string, excludeArray as object) as object
    if excludeArray.IsEmpty() then return nodeArray

    newNodeArray = []
    for each node in nodeArray
        excludeThisNode = false
        for each exclude in excludeArray
            if node[nodeKey] = exclude
                excludeThisNode = true
            end if
        end for
        if excludeThisNode = false
            newNodeArray.Push(node)
        end if
    end for
    return newNodeArray
end function
