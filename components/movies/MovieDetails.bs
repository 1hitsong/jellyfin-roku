import "pkg:/source/utils/misc.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/api/Image.bs"

import "pkg:/source/enums/AnimationControl.bs"
import "pkg:/source/enums/AnimationState.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/ViewLoadStatus.bs"
import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/ImageType.bs"
import "pkg:/source/enums/VideoType.bs"
import "pkg:/source/enums/MediaStreamType.bs"
import "pkg:/source/enums/PersonType.bs"

sub init()
    m.container = m.top.findnode("container")
    m.extrasGrp = m.top.findnode("extrasGrp")
    m.extrasGrid = m.top.findNode("extrasGrid")
    m.top.optionsAvailable = false

    m.options = m.top.findNode("movieOptions")
    m.infoGroup = m.top.findNode("infoGroup")

    m.buttonGrp = m.top.findNode("buttons")
    m.buttonGrp.setFocus(true)
    m.top.lastFocus = m.buttonGrp

    setButtonColors()

    m.loadStatus = ViewLoadStatus.INIT

    ' Only allow Admins to see Edit Subtitles button
    if not m.global.session.user.policy.EnableSubtitleManagement
        editSubtitleButton = m.top.findNode("editSubtitlesButton")
        m.buttonGrp.removeChild(editSubtitleButton)
    end if

    m.top.observeField("itemContent", "itemContentChanged")
end sub

sub setButtonColors()
    buttonList = ["play-button", "options-button", "watched-button", "favorite-button", "editSubtitlesButton", "trailer-button"]

    for each button in buttonList
        buttonNode = m.top.findNode(button)
        buttonNode.background = ColorPalette.TRANSPARENT
        buttonNode.textColor = ColorPalette.WHITE
        buttonNode.focusBackground = ColorPalette.HIGHLIGHT
        buttonNode.focusTextColor = ColorPalette.WHITE
    end for

    firstButton = m.top.findNode(buttonList[0])
    firstButton.setFocus(true)
    firstButton.focus = true
end sub

' OnScreenShown: Callback function when view is presented on screen
'
sub OnScreenShown()
    ' set focus to button group
    if m.extrasGrp.opacity = 1
        m.top.lastFocus.setFocus(true)
    else
        m.buttonGrp.setFocus(true)
    end if

    if m.loadStatus = ViewLoadStatus.INIT
        m.loadStatus = ViewLoadStatus.FIRSTLOAD
        return
    end if

    m.loadStatus = ViewLoadStatus.RELOAD

    m.top.refreshMovieDetailsData = not m.top.refreshMovieDetailsData
end sub

sub trailerAvailableChanged()
    if not m.top.trailerAvailable
        m.buttonGrp.removeChild(m.top.findNode("trailer-button"))
        return
    end if
end sub

' setBackdropImage: If item has a backdrop image, use it as the background for the view
'
' @param {object} itemData - meta data for current item
sub setBackdropImage(itemData as object)
    if isValidAndNotEmpty(itemData.BackdropImageTags)
        imageVersion = ImageType.BACKDROP
        imageTag = itemData.BackdropImageTags[0]
    else
        imageVersion = ImageType.PRIMARY
        imageTag = itemData.ImageTags.Primary
    end if

    movieBackdrop = createObject("roSGNode", "Poster")
    movieBackdrop.loadDisplayMode = "scaleToZoom"
    movieBackdrop.translation = "[0,0]"
    movieBackdrop.width = "1920"
    movieBackdrop.height = "1080"
    movieBackdrop.opacity = .3
    movieBackdrop.uri = ImageURL(m.top.id, imageVersion, {
        "maxWidth": 1920,
        "Tag": imageTag
    })
    m.top.insertChild(movieBackdrop, 0)
end sub

sub onLogoImageURIChange()
    ' We have exhaused all methods for getting a logo to display
    if m.top.logoImageURI = "" and not isChainValid(m.top.itemContent.json, "ImageTags.Logo")
        return
    end if

    movieLogo = createObject("roSGNode", "Poster")
    movieLogo.loadDisplayMode = "scaleToFit"
    movieLogo.translation = [100, 25]
    movieLogo.width = "500"
    movieLogo.height = "250"
    movieLogo.uri = m.top.logoImageURI
    m.top.insertChild(movieLogo, 1)
end sub

' setLogoImage: If item has a logo image, add it to the view
'
' @param {object} itemData - meta data for current item
sub setLogoImage(itemData as object)
    if LCase(itemData.type) = VideoType.EPISODE or LCase(itemData.type) = VideoType.SERIES
        return
    end if

    ' No logo found
    if not isChainValid(itemData, "ImageTags.Logo")
        addTextMovieTitle(itemData)
        return
    end if

    movieLogo = createObject("roSGNode", "Poster")
    movieLogo.loadDisplayMode = "scaleToFit"
    movieLogo.translation = [100, 25]
    movieLogo.width = "500"
    movieLogo.height = "250"
    movieLogo.uri = ImageURL(m.top.id, ImageType.LOGO, {
        "maxHeight": 300,
        "maxWidth": 600,
        "Tag": itemData.ImageTags.Logo
    })

    m.top.insertChild(movieLogo, 1)
end sub

sub addTextMovieTitle(itemData as object)
    movieTitle = createObject("roSGNode", "Label")
    movieTitle.font = "font:LargeSystemFont"
    movieTitle.font.size = 70
    movieTitle.text = itemData.name
    movieTitle.translation = [100, 210]
    movieTitle.width = 1700

    m.top.insertChild(movieTitle, 1)
end sub

sub itemContentChanged()
    ' Updates video metadata
    item = m.top.itemContent

    if not isChainValid(item, "json")
        m.buttonGrp.visible = true
        stopLoadingSpinner()
        return
    end if

    itemData = item.json
    m.top.id = itemData.id

    if itemData.UserData.PlaybackPositionTicks > 0
        playButton = m.top.findNode("play-button")
        playButton.text = `Play / Resume from ${ticksToHuman(itemData.UserData.PlaybackPositionTicks)}`
    end if
    setWatchedColor()
    setFavoriteColor()

    ' We don't need to update everything if this is a reload
    if m.loadStatus = ViewLoadStatus.RELOAD then return

    setBackdropImage(itemData)
    setLogoImage(itemData)

    ' Set default video source if user hasn't selected one yet
    if m.top.selectedVideoStreamId = "" and isValid(itemData.MediaSources)
        m.top.selectedVideoStreamId = itemData.MediaSources[0].id
    end if

    ' Find first Audio Stream and set that as default
    SetDefaultAudioTrack(itemData)

    ' Handle all "As Is" fields
    setFieldText("releaseYear", itemData.productionYear)
    setFieldText("overview", itemData.overview)

    if isValid(itemData.officialRating)
        setFieldText("officialRating", itemData.officialRating)
    else
        m.infoGroup.removeChild(m.top.findNode("officialRating"))
    end if

    if m.global.session.user.settings["ui.movies.showRatings"]
        if isValid(itemData.communityRating)
            setFieldText("communityRating", int(itemData.communityRating * 10) / 10)
        else
            m.infoGroup.removeChild(m.top.findNode("communityRatingGroup"))
        end if
        if isValid(itemData.CriticRating)
            setFieldText("criticRatingLabel", itemData.criticRating)
            if itemData.CriticRating > 60
                tomato = "pkg:/images/fresh.png"
            else
                tomato = "pkg:/images/rotten.png"
            end if
            criticRatingIcon = m.top.findNode("criticRatingIcon")
            if isValid(criticRatingIcon) then criticRatingIcon.uri = tomato
        else
            m.infoGroup.removeChild(m.top.findNode("criticRatingGroup"))
        end if
    else
        m.infoGroup.removeChild(m.top.findNode("communityRatingGroup"))
        m.infoGroup.removeChild(m.top.findNode("criticRatingGroup"))
    end if

    if type(itemData.RunTimeTicks) = "LongInteger"
        setFieldText("runtime", stri(getRuntime()) + " mins")
        if m.global.session.user.settings["ui.design.hideclock"] <> true
            setFieldText("ends-at", tr("Ends at %1").Replace("%1", getEndTime()))
        end if
    end if

    if isValidAndNotEmpty(itemData.genres)
        setFieldText("genres", itemData.genres[0])
    else
        dot = m.top.findNode("dot")
        dot.getParent().removeChild(dot)

        genres = m.top.findNode("genres")
        genres.getParent().removeChild(genres)
    end if

    directors = []
    for each person in itemData.people
        if LCase(person.type) = PersonType.DIRECTOR
            directors.push(person.name)
        end if
    end for

    if isValidAndNotEmpty(directors)
        setFieldText("director", tr("Director") + ": " + directors.join(", "))
    else
        m.top.findNode("director").getParent().removeChild(m.top.findNode("director"))
    end if

    'set aired date if type is Episode
    if LCase(itemData.Type) = VideoType.EPISODE
        if isValid(itemData.PremiereDate)
            airDate = CreateObject("roDateTime")
            airDate.FromISO8601String(itemData.PremiereDate)
            m.top.findNode("aired").text = tr("Aired") + ": " + airDate.AsDateString("short-month-no-weekday")
            'remove movie release year label
            m.infoGroup.removeChild(m.top.findNode("releaseYear"))
        else
            m.infoGroup.removeChild(m.top.findNode("aired"))
        end if

        addEpisodeTitle(itemData)
    else
        m.infoGroup.removeChild(m.top.findNode("aired"))
    end if

    SetUpVideoOptions(itemData.mediaSources)
    SetUpAudioOptions(itemData.mediaStreams)

    m.buttonGrp.visible = true
    stopLoadingSpinner()
end sub

sub addEpisodeTitle(itemData as object)
    episodeTitle = createObject("roSGNode", "Label")
    episodeTitle.font = "font:MediumBoldSystemFont"
    episodeTitle.text = `Season ${itemData.ParentIndexNumber} Episode ${itemData.IndexNumber} â€¢ ${itemData.name}`

    m.container.insertChild(episodeTitle, 0)
end sub

sub SetUpVideoOptions(streams)

    videos = []
    codecDetailsSet = false

    for i = 0 to streams.Count() - 1
        if streams[i].VideoType = "VideoFile"
            codec = ""
            if isValidAndNotEmpty(streams[i].mediaStreams)

                ' find the first (default) video track to get the codec for the details screen
                if codecDetailsSet = false
                    for index = 0 to streams[i].mediaStreams.Count() - 1
                        if streams[i].mediaStreams[index].Type = MediaStreamType.VIDEO
                            setFieldText("video_codec", tr("Video") + ": " + streams[i].mediaStreams[index].displayTitle)
                            codecDetailsSet = true
                            exit for
                        end if
                    end for
                end if

                codec = streams[i].mediaStreams[0].displayTitle
            end if

            ' Create options for user to switch between video tracks
            videos.push({
                "Title": streams[i].Name,
                "Description": tr("Video"),
                "Selected": m.top.selectedVideoStreamId = streams[i].id,
                "StreamID": streams[i].id,
                "video_codec": codec
            })
        end if
    end for

    if streams.count() > 1
        m.top.findnode("video_codec_count").text = "+" + stri(streams.Count() - 1).trim()
    end if

    options = {}
    options.videos = videos
    m.options.options = options
end sub


sub SetUpAudioOptions(streams)
    tracks = []

    for i = 0 to streams.Count() - 1
        if streams[i].Type = "Audio"
            tracks.push({ "Title": streams[i].displayTitle, "Description": streams[i].Title, "Selected": m.top.selectedAudioStreamIndex = i, "StreamIndex": i })
        end if
    end for

    if tracks.count() > 1
        m.top.findnode("audio_codec_count").text = "+" + stri(tracks.Count() - 1).trim()
    end if

    options = {}
    if isValid(m.options.options.videos)
        options.videos = m.options.options.videos
    end if
    options.audios = tracks
    m.options.options = options

end sub


sub SetDefaultAudioTrack(itemData)
    for i = 0 to itemData.mediaStreams.Count() - 1
        if itemData.mediaStreams[i].Type = "Audio"
            m.top.selectedAudioStreamIndex = i
            setFieldText("audio_codec", tr("Audio") + ": " + itemData.mediaStreams[i].displayTitle + "asd adsf asdf asdf")
            exit for
        end if
    end for
end sub

sub setFieldText(field, value)
    node = m.top.findNode(field)
    if not isAllValid([node, value]) then return

    ' Handle non strings... Which _shouldn't_ happen, but hey
    if type(value) = "roInt" or type(value) = "Integer"
        value = str(value)
    else if type(value) = "roFloat" or type(value) = "Float"
        value = str(value)
    else if type(value) <> "roString" and type(value) <> "String"
        value = ""
    end if

    node.text = value
end sub

function getRuntime() as integer

    itemData = m.top.itemContent.json

    ' A tick is .1ms, so 1/10,000,000 for ticks to seconds,
    ' then 1/60 for seconds to minutess... 1/600,000,000
    return round(itemData.RunTimeTicks / 600000000.0)
end function

function getEndTime() as string
    itemData = m.top.itemContent.json

    date = CreateObject("roDateTime")
    duration_s = int(itemData.RunTimeTicks / 10000000.0)
    date.fromSeconds(date.asSeconds() + duration_s)
    date.toLocalTime()

    return formatTime(date)
end function

sub setFavoriteColor()
    fave = m.top.itemContent.favorite
    fave_button = m.top.findNode("favorite-button")
    if isValid(fave) and fave
        fave_button.iconBlendColor = ColorPalette.RED
        fave_button.text = tr("Favorited")
    else
        fave_button.iconBlendColor = ColorPalette.WHITE
        fave_button.text = tr("Mark As Favorite")
    end if
end sub

sub setWatchedColor()
    watched = m.top.itemContent.watched
    watched_button = m.top.findNode("watched-button")
    if watched
        watched_button.text = tr("Watched")
    else
        watched_button.text = tr("Mark As Watched")
    end if
end sub

function round(f as float) as integer
    ' BrightScript only has a "floor" round
    ' This compares floor to floor + 1 to find which is closer
    m = int(f)
    n = m + 1
    x = abs(f - m)
    y = abs(f - n)
    if y > x
        return m
    else
        return n
    end if
end function

'
'Check if options updated and any reloading required
sub audioOptionsClosed()
    if m.options.audioStreamIndex <> m.top.selectedAudioStreamIndex
        m.top.selectedAudioStreamIndex = m.options.audioStreamIndex
        setFieldText("audio_codec", tr("Audio") + ": " + m.top.itemContent.json.mediaStreams[m.top.selectedAudioStreamIndex].displayTitle)
    end if
    m.top.findNode("buttons").setFocus(true)
end sub

'
' Check if options were updated and if any reloding is needed...
sub videoOptionsClosed()
    if m.options.videoStreamId <> m.top.selectedVideoStreamId
        m.top.selectedVideoStreamId = m.options.videoStreamId
        setFieldText("video_codec", tr("Video") + ": " + m.options.video_codec)
        ' Because the video stream has changed (i.e. the actual video)... we need to reload the audio stream choices for that video
        m.top.unobservefield("itemContent")
        itemData = m.top.itemContent.json
        for each mediaSource in itemData.mediaSources
            if mediaSource.id = m.top.selectedVideoStreamId
                itemData.mediaStreams = []
                for i = 0 to mediaSource.mediaStreams.Count() - 1
                    itemData.mediaStreams.push(mediaSource.mediaStreams[i])
                end for
                SetDefaultAudioTrack(itemData)
                SetUpAudioOptions(itemData.mediaStreams)
                exit for
            end if
        end for
        m.top.itemContent.json = itemData
        m.top.observeField("itemContent", "itemContentChanged")
    end if
    m.top.findNode("buttons").setFocus(true)
end sub

sub onButtonGroupEscape()
    if LCase(m.buttonGrp.escape) = "down"
        m.top.lastFocus = m.extrasGrid
        m.extrasGrid.setFocus(true)
        m.top.findNode("VertSlider").reverse = false
        m.top.findNode("extrasFader").reverse = false
        m.top.findNode("pplAnime").control = AnimationControl.START
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean

    if key = KeyCode.UP and m.top.findNode("extrasGrid").isInFocusChain()
        if m.extrasGrid.itemFocused = 0
            m.top.lastFocus = m.buttonGrp
            m.top.findNode("VertSlider").reverse = true
            m.top.findNode("extrasFader").reverse = true
            m.top.findNode("pplAnime").control = AnimationControl.START
            m.buttonGrp.setFocus(true)
            m.buttonGrp.getChild(m.buttonGrp.getChildCount() - 1).focus = true
            return true
        end if

        return false
    end if

    if not press then return false

    if key = KeyCode.OK and m.buttonGrp.hasFocus()
        m.loadStatus = ViewLoadStatus.RELOAD

        buttonList = m.buttonGrp.getChildren(-1, 0)

        i = 0
        for each button in buttonList
            if button.focus
                m.top.buttonSelected = button.id
                if button.id = "options-button"
                    m.options.visible = true
                    m.options.setFocus(true)
                end if
                exit for
            end if
            i++
        end for

        return true
    end if

    if key = KeyCode.DOWN and m.buttonGrp.hasFocus()
        buttonList = m.buttonGrp.getChildren(-1, 0)

        i = 0
        for each button in buttonList
            if button.focus
                button.focus = false
                if i + 1 < m.buttonGrp.getChildCount()
                    nextButton = m.buttonGrp.getChild(i + 1)
                    if isValid(nextButton)
                        nextButton.focus = true
                    end if
                else
                    m.top.lastFocus = m.extrasGrid
                    m.extrasGrid.setFocus(true)
                    m.top.findNode("VertSlider").reverse = false
                    m.top.findNode("extrasFader").reverse = false
                    m.top.findNode("pplAnime").control = AnimationControl.START
                end if
                exit for
            end if
            i++
        end for

        return true
    end if

    if key = KeyCode.UP and m.buttonGrp.hasFocus()
        buttonList = m.buttonGrp.getChildren(-1, 0)

        i = 0
        for each button in buttonList
            if button.focus
                if i - 1 >= 0
                    button.focus = false
                    nextButton = m.buttonGrp.getChild(i - 1)
                    if isValid(nextButton)
                        nextButton.focus = true
                    end if
                end if
                exit for
            end if
            i++
        end for

        return true
    end if

    if key = KeyCode.BACK
        if m.options.visible = true
            m.options.visible = false
            videoOptionsClosed()
            audioOptionsClosed()
            return true
        end if

        if m.top.findNode("extrasGrid").isInFocusChain()
            m.top.lastFocus = m.buttonGrp
            m.top.findNode("VertSlider").reverse = true
            m.top.findNode("extrasFader").reverse = true
            m.top.findNode("pplAnime").control = AnimationControl.START
            m.buttonGrp.setFocus(true)
            m.buttonGrp.getChild(m.buttonGrp.getChildCount() - 1).focus = true
            return true
        end if

        return false
    end if

    if key = KeyCode.PLAY and m.extrasGrid.hasFocus()
        if isValid(m.extrasGrid.focusedItem)
            m.top.quickPlayNode = m.extrasGrid.focusedItem
            return true
        end if

        return false
    end if

    return false
end function
