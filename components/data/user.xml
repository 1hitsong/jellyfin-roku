<?xml version="1.0" encoding="utf-8" ?>
<component name="UserData" extends="ContentNode">
  <interface>
    <field id="id" type="string" />
    <field id="username" type="string" />
    <field id="token" type="string" />
    <field id="server" type="string" />
    <field id="port" type="string" />
    <field id="json" type="associativearray" onChange="setDataFromJSON" />
    <function name="setServer" />
    <function name="getPreference" />
    <function name="setPreference" />
    <function name="loadFromRegistry" />
    <function name="saveToRegistry" />
    <function name="setActive" />
  </interface>
  <script type="text/brightscript" uri="pkg:/source/utils/config.brs" />
  <script type="text/brightscript">
  <![CDATA[
  sub setDataFromJSON()
    json = m.top.json
    loadFromJSON(json)
  end sub

  function loadFromJSON(json)
    m.top.id = json.User.id

    m.top.username = json.User.username
    m.top.token = json.AccessToken
  end function

  function loadFromRegistry(id as String)
    m.top.id = id

    m.top.username = get_user_setting("username")
    m.top.token = get_user_setting("token")
  end function

  function saveToRegistry()
    set_user_setting("username", m.top.username)
    set_user_setting("token", m.top.token)

    users = get_setting("available_users", {})
    this_user = invalid
    for each user in users
      if user.id = m.top.id then this_user = user
    end for
    if this_user = invalid
      users[m.top.id] = {
        id: m.top.id,
        username: m.top.username,
        server: get_setting("server"),
        port: get_setting("port")
      }
      set_setting("available_users", users)
    end if

  end function

  function getPreference(key as String, default as String)
    return get_user_setting("pref-" + key, default)
  end function

  function setPreference(key as String, value as String)
    return set_user_setting("pref-" + key, value)
  end function

  function setActive()
    set_setting("active_user", m.top.id)
  end function

  function setServer(hostname as string, port as string)
    m.top.server = hostname
    m.top.port = port
  end function
  ]]>
  </script>
</component>
