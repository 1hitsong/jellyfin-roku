import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.content = m.top.findNode("content")

    setPalette()

    m.top.id = "OKDialog"
    m.top.height = 900
    m.top.title = `What's New In ${m.global.app.version}`
    m.top.buttons = [tr("OK")]

    dialogStyles = {
        "default": {
            "fontSize": 27,
            "fontUri": "font:SystemFontFile",
            "color": "#EFEFEFFF"
        },
        "b": {
            "fontSize": 27,
            "fontUri": "font:SystemFontFile",
            "color": ColorPalette.TRIADGREEN
        },
        "author": {
            "fontSize": 27,
            "fontUri": "font:SystemFontFile",
            "color": chainLookupReturn(m.global.session, "user.settings.colorWhatsNewAuthor", ColorPalette.HIGHLIGHT)
        }
    }

    whatsNewList = ParseJSON(ReadAsciiFile("pkg:/source/static/whatsNew/" + m.global.app.version.ToStr().trim() + ".json"))

    for each item in whatsNewList
        textLine = m.content.CreateChild("StdDlgMultiStyleTextItem")
        textLine.drawingStyles = dialogStyles
        textLine.text = "â€¢ " + item.description + " <author>" + item.author + "</author>"
    end for
end sub

sub setPalette()
    dlgPalette = createObject("roSGNode", "RSGPalette")
    dlgPalette.colors = {
        DialogBackgroundColor: "0x262828FF",
        DialogFocusColor: "0xcececeFF",
        DialogFocusItemColor: "0x202020FF",
        DialogSecondaryTextColor: "0xf8f8f8ff",
        DialogSecondaryItemColor: chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT),
        DialogTextColor: "0xeeeeeeFF"
    }

    m.top.palette = dlgPalette
end sub
